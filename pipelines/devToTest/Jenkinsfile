pipeline {
    agent any
    
    environment {
        ORACLE_CONTAINER = "oracle-db"
        SQLCL_PATH       = "/opt/sqlcl/bin/sql"
        DB_CONN_STR      = "localhost:1521/FREEPDB1"
        BUILD_DIR        = "/home/oracle/build_cicd"
        
        SONAR_SCANNER_HOME = tool name: 'SonarScanner', type: 'hudson.plugins.sonar.SonarRunnerInstallation'
        SONAR_PROJECT_KEY = "apex-project-${env.BRANCH_NAME}"
        SONAR_PROJECT_NAME = "APEX Project - ${env.BRANCH_NAME}"
    }
    
    triggers {
        githubPush()
    }
    
    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }
        
        stage('SonarQube Analysis') {
            when { branch 'develop' }
            steps {
                script {
                    echo "=== Análisis de Código con SonarQube (Solo Develop) ==="
                    withSonarQubeEnv('SonarQube') {
                        sh """
                            /${SONAR_SCANNER_HOME}/bin/sonar-scanner \
                                -Dsonar.projectKey=${SONAR_PROJECT_KEY} \
                                -Dsonar.projectName="${SONAR_PROJECT_NAME}" \
                                -Dsonar.projectVersion=${BUILD_NUMBER} \
                                -Dsonar.sources=. \
                                -Dsonar.inclusions=**/*.sql,**/*.pks,**/*.pkb,**/*.fnc,**/*.prc,**/*.trg \
                                -Dsonar.exclusions=**/node_modules/**,**/target/**,**/artifact/** \
                                -Dsonar.plsql.file.suffixes=sql,pks,pkb,fnc,prc,trg \
                                -Dsonar.language=plsql \
                                -Dsonar.sourceEncoding=UTF-8 \
                                -Dsonar.working.directory=${WORKSPACE}/sonar-reports
                        """
                    }
                }
            }
        }
        
        stage('Quality Gate') {
            when { branch 'develop' }
            steps {
                script {
                    echo "=== Esperando resultado de Quality Gate (Webhook) ==="
                    timeout(time: 10, unit: 'MINUTES') {
                        def qg = waitForQualityGate()
                        if (qg.status != 'OK') {
                            error "El análisis de calidad no pasó los umbrales: ${qg.status}"
                        } else {
                            echo "Quality Gate PASSED"
                        }
                    }
                }
            }
        }
        
        stage('Generate Artifact') {
            environment {
                DB = credentials('apex-dev-credential')
            }
            steps {
                script {
                    echo "=== Generando Artefacto en contenedor Oracle ==="
                    
                    // Aseguramos directorio base
                    sh "docker exec -u root ${ORACLE_CONTAINER} mkdir -p ${BUILD_DIR}"
                    sh "docker exec -u root ${ORACLE_CONTAINER} chown -R root:root ${BUILD_DIR}"
                    sh "docker exec -u root ${ORACLE_CONTAINER} chmod -R 777 ${BUILD_DIR}"
                    
                    // Copiamos código
                    sh "docker cp . ${ORACLE_CONTAINER}:${BUILD_DIR}/"
                    
                    // Script para generar artefacto
                    // Incluimos la actualización de versión DENTRO de SQLcl para mayor seguridad
                    def genScript = """
                        cd ${BUILD_DIR}
                        
                        # Fix Version Mismatch
                        ${SQLCL_PATH} /nolog <<EOF
                        cd ${BUILD_DIR}
                        project config set -name sqlcl.version -value 25.4.0.0
                        exit
EOF
                        
                        # Generar Artefacto
                        ${SQLCL_PATH} \${DB_USR}/\${DB_PSW}@${DB_CONN_STR} <<EOF
                        project gen-artifact -name app-${BRANCH_NAME} -version ${BUILD_NUMBER}
                        exit
EOF
                    """
                    writeFile file: 'gen_artifact.sh', text: genScript
                    
                    // Transferir y dar permisos al script
                    sh "docker cp gen_artifact.sh ${ORACLE_CONTAINER}:${BUILD_DIR}/gen_artifact.sh"
                    // Corrección importante: usamos 'oracle:' para asignar el grupo por defecto del usuario
                    sh "docker exec -u root ${ORACLE_CONTAINER} chown oracle: ${BUILD_DIR}/gen_artifact.sh"
                    sh "docker exec -u root ${ORACLE_CONTAINER} chmod +x ${BUILD_DIR}/gen_artifact.sh"
                    
                    // Cambiar propietario del directorio a oracle para permisos de escritura
                    sh "docker exec -u root ${ORACLE_CONTAINER} chown -R oracle: ${BUILD_DIR}"
                    
                    // Ejecución segura: variables de entorno + comillas simples
                    sh 'docker exec -u oracle -e DB_USR=$DB_USR -e DB_PSW=$DB_PSW ' + ORACLE_CONTAINER + " bash ${BUILD_DIR}/gen_artifact.sh"
                    
                    // Debug: Verificar estructura generada
                    sh "echo '--- Debug: Listado del directorio de build ---'"
                    sh "docker exec -u root ${ORACLE_CONTAINER} ls -laR ${BUILD_DIR}"
                    
                    sh "mkdir -p artifact"
                    sh "docker cp ${ORACLE_CONTAINER}:${BUILD_DIR}/artifact/. artifact/ || echo 'No se pudo copiar el artefacto'"
                    
                    // Debug: Verificar el artefacto copiado localmente
                    sh "echo '--- Debug: Listado local de artifacts ---'"
                    sh "ls -la artifact/"
               }
            }
        }
        
        stage('Deploy to DEV') {
            when { branch 'develop' }
            environment {
                DB = credentials('apex-dev-credential')
            }
            steps {
                script {
                    echo "=== Desplegando a Desarrollo ==="
                    
                    def artifactFile = sh(script: "ls artifact/*.zip | head -n 1", returnStdout: true).trim()
                    if (!artifactFile) {
                        error "No se encontró ningún archivo .zip en la carpeta artifact/. Abortando deploy."
                    }
                    
                    def deployScript = """
                        cd ${BUILD_DIR}
                        ${SQLCL_PATH} \${DB_USR}/\${DB_PSW}@${DB_CONN_STR} <<EOF
                        project deploy -file /home/oracle/deploy_app.zip
                        exit
EOF
                    """
                    writeFile file: 'deploy_dev.sh', text: deployScript
                    
                    // Transferencia de archivos
                    sh "docker cp ${artifactFile} ${ORACLE_CONTAINER}:/home/oracle/deploy_app.zip"
                    sh "docker cp deploy_dev.sh ${ORACLE_CONTAINER}:${BUILD_DIR}/deploy.sh"
                    
                    sh "docker exec -u root ${ORACLE_CONTAINER} chown oracle: /home/oracle/deploy_app.zip ${BUILD_DIR}/deploy.sh"
                    
                    // Ejecución segura
                    sh 'docker exec -u oracle -e DB_USR=$DB_USR -e DB_PSW=$DB_PSW ' + ORACLE_CONTAINER + " bash ${BUILD_DIR}/deploy.sh"
                }
            }
        }

        stage('Deploy to TEST') {
            when { branch 'test' }
            environment {
                DB = credentials('apex-test-credential')
            }
            steps {
                script {
                    echo "=== Desplegando a TEST ==="
                    
                    def artifactFile = sh(script: "ls artifact/*.zip | head -n 1", returnStdout: true).trim()
                    if (!artifactFile) {
                        error "No se encontró ningún archivo .zip en la carpeta artifact/. Abortando deploy."
                    }
                    
                    def deployScript = """
                        cd ${BUILD_DIR}
                        ${SQLCL_PATH} \${DB_USR}/\${DB_PSW}@${DB_CONN_STR} <<EOF
                        DEFINE DEFAULTS_FILE=dist/utils/properties/test.properties
                        project deploy -file /home/oracle/deploy_app.zip
                        exit
EOF
                    """
                    writeFile file: 'deploy_test.sh', text: deployScript
                    
                    sh "docker cp ${artifactFile} ${ORACLE_CONTAINER}:/home/oracle/deploy_app.zip"
                    sh "docker cp deploy_test.sh ${ORACLE_CONTAINER}:${BUILD_DIR}/deploy.sh"
                    
                    sh "docker exec -u root ${ORACLE_CONTAINER} chown oracle: /home/oracle/deploy_app.zip ${BUILD_DIR}/deploy.sh"
                    
                    // Ejecución segura
                    sh 'docker exec -u oracle -e DB_USR=$DB_USR -e DB_PSW=$DB_PSW ' + ORACLE_CONTAINER + " bash ${BUILD_DIR}/deploy.sh"
                }
            }
        }
    }
    
    post {
        success {
            emailext(
                subject: "[OK] Pipeline Exitoso - #${BUILD_NUMBER}",
                body: "El despliegue de la rama ${env.BRANCH_NAME} se completó con éxito.\nLogs: ${env.BUILD_URL}",
                to: "sergiolucero73@gmail.com",
                mimeType: 'text/plain'
            )
        }
        failure {
            emailext(
                subject: "[FAIL] Pipeline Fallido - #${BUILD_NUMBER}",
                body: "El despliegue de la rama ${env.BRANCH_NAME} ha fallado.\nLogs: ${env.BUILD_URL}console",
                to: "sergiolucero73@gmail.com",
                mimeType: 'text/plain'
            )
        }
        always {
            script {
                sh "/usr/bin/docker exec -u root ${ORACLE_CONTAINER} rm -rf ${BUILD_DIR} /home/oracle/deploy_app.zip || true"
                archiveArtifacts artifacts: 'artifact/*.zip', allowEmptyArchive: true
            }
        }
    }
}