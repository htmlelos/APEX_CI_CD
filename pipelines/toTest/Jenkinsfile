pipeline {
    agent any
    
    triggers {
        githubPush()
    }

    options {
        skipDefaultCheckout()
        timeout(time: 30, unit: 'MINUTES')
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }

    environment {
        ORACLE_CONTAINER = "oracle-db"
        SQLCL_PATH       = "/opt/sqlcl/bin/sql"
        DB_CONN_STR      = "localhost:1521/FREEPDB1"
        BUILD_DIR        = "/tmp/apex_project_test"
        DB_USR           = "TEST_SCHEMA"
        APEX_TEST_URL    = "http://localhost:8181/ords/r/test_wrkspc_cicd/app-ci-cd" 
        MAIL_RECIPIENTS  = "sergiolucero73@gmail.com, slucero@runaid.com.ar"
    }

    stages {
        stage('Initialize') {
            steps {
                deleteDir()
                checkout scm
            }
        }

        stage('Obtain Artifact from DEVELOP') {
            steps {
                script {
                    echo "=== Obteniendo Artefacto (.zip) desde DEVELOP ==="
                    // Esta sección es la que 'toma' el artefacto generado en toDevelop
                    sh "mkdir -p artifact"
                    copyArtifacts(
                        projectName: 'APEX-Deploy-DESA',
                        selector: lastSuccessful(),
                        filter: 'artifact/*.zip',
                        target: 'artifact',
                        flatten: true
                    )
                    sh "ls -la artifact/"

                    echo "=== Preparando Contexto del Proyecto (Config y Propiedades) en Contenedor ==="
                    // El .tar NO es el artefacto de la aplicación.
                    // Es solo para mover la configuración (.dbtools, properties) al contenedor para que SQLcl funcione.
                    sh """
                        tar --exclude='./artifact' -cf /tmp/project_context.tar -C ${WORKSPACE} .
                        docker exec -u root ${ORACLE_CONTAINER} mkdir -p ${BUILD_DIR}
                        docker cp /tmp/project_context.tar ${ORACLE_CONTAINER}:/tmp/
                        docker exec -u root ${ORACLE_CONTAINER} bash -c "
                            rm -rf ${BUILD_DIR}/* ${BUILD_DIR}/.* 2>/dev/null || true
                            tar -xf /tmp/project_context.tar -C ${BUILD_DIR}
                            chown -R oracle: ${BUILD_DIR}
                            rm /tmp/project_context.tar
                        "
                        rm /tmp/project_context.tar
                    """
                }
            }
        }

        stage('Deploy to TEST') {
            environment {
                DB = credentials('apex-test-credential')
            }
            steps {
                script {
                    echo "=== Desplegando a TEST (Esquema: ${DB_USR}) ==="
                    def artifactFile = sh(script: "ls artifact/*.zip | head -n 1", returnStdout: true).trim()
                    def workspaceName = sh(script: "grep 'apex.102.workspace' dist/utils/properties/test.properties | cut -d= -f2 | tr -d '\\r'", returnStdout: true).trim()

                    echo "=== Verificando Existencia del Workspace: ${workspaceName} ==="
                    sh """
                        docker exec -u oracle -e DB_USR="\$DB_USR" -e DB_PSW="\$DB_PSW" ${ORACLE_CONTAINER} bash -c "
                            ${SQLCL_PATH} -S \"\$DB_USR\"/\"\$DB_PSW\"@${DB_CONN_STR} <<EOF
                            SET TERMOUT OFF
                            SET FEEDBACK OFF
                            SET PAGESIZE 0
                            SELECT 'EXISTE_WORKSPACE:' || workspace_id FROM apex_workspaces WHERE workspace = upper('${workspaceName}');
                            EXIT
EOF
                        " | grep "EXISTE_WORKSPACE" || (echo "!!! ERROR: El workspace ${workspaceName} NO existe en el ambiente TEST !!!"; exit 1)
                    """

                    echo "=== Generando Script de Despliegue (File-Based) ==="
                    def deployScript = """
WHENEVER SQLERROR EXIT SQL.SQLCODE
WHENEVER OSERROR EXIT FAILURE
project config set -name sqlcl.connectionName -value test

PROMPT === Redefiniendo Workspace destino: ${workspaceName} ===
BEGIN 
    APEX_APPLICATION_INSTALL.SET_WORKSPACE('${workspaceName}'); 
END;
/

project deploy -file /tmp/deploy_app.zip
exit
""".trim()
                    writeFile file: 'deploy_script.sql', text: deployScript

                    sh "docker cp ${artifactFile} ${ORACLE_CONTAINER}:/tmp/deploy_app.zip"
                    sh "docker cp deploy_script.sql ${ORACLE_CONTAINER}:${BUILD_DIR}/deploy_script.sql"

                    sh """
                        docker exec -u oracle -e DB_USR="\$DB_USR" -e DB_PSW="\$DB_PSW" ${ORACLE_CONTAINER} bash -c '
                            cd ${BUILD_DIR}
                            echo "=== Preparando Configuración de Liquibase ==="
                            sed "s/\\\\r//" dist/utils/properties/test.properties | grep "=" > liquibase.properties
                            
                            echo "=== Ejecutando Despliegue con SQLcl ==="
                            ${SQLCL_PATH} "\$DB_USR"/"\$DB_PSW"@${DB_CONN_STR} @deploy_script.sql | tee /tmp/sqlcl_deploy.log
                            
                            if grep -qiE "ERROR:|Bad Filename|Usage:|not recognized|Failure|ORA-" /tmp/sqlcl_deploy.log; then
                                echo "!!! ERROR DETECTADO EN EL DESPLIEGUE !!!"
                                exit 1
                            fi
                        '
                    """
                }
            }
        }

    }
    
    post {
        success {
            script {
                try {
                    mail to: "${env.MAIL_RECIPIENTS}",
                         subject: "Success: Pipeline ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                         body: "La ejecución del pipeline ${env.JOB_NAME} #${env.BUILD_NUMBER} fue exitosa.\nDespliegue en TEST completado.\n\nVer detalles en: ${env.BUILD_URL}"
                } catch (Exception e) {
                    echo "Error enviando correo: ${e.message}"
                }
            }
        }
        failure {
            script {
                try {
                    mail to: "${env.MAIL_RECIPIENTS}",
                         subject: "Failure: Pipeline ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                         body: "La ejecución del pipeline ${env.JOB_NAME} #${env.BUILD_NUMBER} ha fallado en el ambiente de TEST.\n\nVer consola: ${env.BUILD_URL}console"
                } catch (Exception e) {
                    echo "Error enviando correo de fallo: ${e.message}"
                }
            }
        }
        always {
            archiveArtifacts artifacts: 'artifact/*.zip', allowEmptyArchive: true
            sh "docker exec -u root ${ORACLE_CONTAINER} rm -rf ${BUILD_DIR} || true"
        }
    }
}
